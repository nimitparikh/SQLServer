USE [master];
GO
IF EXISTS
(
    SELECT *
    FROM sys.objects
    WHERE object_id = OBJECT_ID(N'[dbo].[spTempDBConfig]')
          AND type IN(N'P', N'PC')
)
    DROP PROCEDURE [dbo].[spTempDBConfig];
GO
SET ANSI_NULLS ON;
GO
SET QUOTED_IDENTIFIER ON;
GO
IF NOT EXISTS
(
    SELECT *
    FROM sys.objects
    WHERE object_id = OBJECT_ID(N'[dbo].[spTempDBConfig]')
          AND type IN(N'P', N'PC')
)
    BEGIN
        EXEC dbo.sp_executesql
             @statement = N'CREATE PROCEDURE [dbo].[spTempDBConfig] AS';
    END;
GO
ALTER PROCEDURE [dbo].[spTempDBConfig]
(@InstanceCount INT = 1,
 @MinPCTFree    INT = 30,
 @AutoGrowthON  BIT = 1,
 @PrintOnly     BIT = 1
)
AS
     DECLARE @cpu_count INT, @file_count INT, @name NVARCHAR(128), @logical_name SYSNAME, @file_name NVARCHAR(520), @physical_name NVARCHAR(520), @drivesize INT, @size INT, @logsize INT, @max_size INT, @growth INT, @alter_command NVARCHAR(MAX);
     SELECT @physical_name = physical_name
     FROM tempdb.sys.database_files
     WHERE name = 'tempdev';
     SELECT @file_count = COUNT(*)
     FROM tempdb.sys.database_files
     WHERE type_desc = 'ROWS';
     SELECT @cpu_count = cpu_count
     FROM sys.dm_os_sys_info;
     PRINT '--Total CPU Count: '+CONVERT(VARCHAR(4), @cpu_count);
     PRINT '--Total Current File Count: '+CONVERT(VARCHAR(4), @file_count);
     IF @cpu_count > 8
         SET @cpu_count = 8;
     SELECT @size = total_bytes / 1024 / 1024 / (@cpu_count + 1) * (100 - @MinPctFree) / 100 / @InstanceCount,
            @drivesize = total_bytes / 1024 / 1024
     FROM sys.dm_os_volume_stats(2, 1);
     SET @size = FLOOR(@size / 256.0) * 256;
     PRINT '--Total Drive size is: '+CONVERT(VARCHAR(20), @drivesize)+' MB';
     PRINT '--Data file size will be '+CONVERT(VARCHAR(8), @size)+' MB';
     IF(@size > 20480)
         SET @growth = 4096;
         ELSE
     IF(@size > 4096)
         SET @growth = 2048;
         ELSE
     SET @growth = 512;
     IF(@size > 8192)
         SET @logsize = 8192;
         ELSE
     SET @logsize = @size;
     IF @AutoGrowthON = 0
         SET @growth = 0;
     PRINT '--Log File size will be '+CONVERT(VARCHAR(20), @logsize)+' MB';
     PRINT '--Data/Log File Growth will be '+CONVERT(VARCHAR(20), @growth)+' MB';
     PRINT '--Free Disk Space will be '+CONVERT(VARCHAR(20), @drivesize - @cpu_count * @size - @logsize)+' MB';
     WHILE @file_count < @cpu_count -- Add * 0.25 here to add 1 file for every 4 cpus, * .5 for every 2 etc.
         BEGIN
             SELECT @logical_name = 'tempdev'+CAST(@file_count AS NVARCHAR);
             SELECT @file_name = REPLACE(@physical_name, 'tempdb.mdf', @logical_name+'.ndf');
             SELECT @alter_command = 'ALTER DATABASE [tempdb] ADD FILE ( NAME =N'''+@logical_name+''', FILENAME =N'''+@file_name+''', SIZE = '+CAST(@size AS NVARCHAR)+'MB, FILEGROWTH = '+CAST(@growth AS NVARCHAR)+'MB )';
             IF @PrintOnly = 1
                 BEGIN
                     PRINT @alter_command;
                 END;
                 ELSE
                 BEGIN
                     PRINT @alter_command;
                     EXEC sp_executesql
                          @alter_command;
                 END;
             SELECT @file_count = @file_count + 1;
         END;
     DECLARE db_cursor CURSOR
     FOR SELECT name
         FROM sys.master_files
         WHERE database_id = 2
               AND (size <> @size * 128.0
                    OR growth <> @growth * 128.0)
               AND type = 0
         UNION
         SELECT name
         FROM sys.master_files
         WHERE database_id = 2
               AND (size > @logsize * 128.0
                    OR growth <> @growth * 128.0)
               AND type = 1;
     OPEN db_cursor;
     FETCH NEXT FROM db_cursor INTO @name;
     WHILE @@FETCH_STATUS = 0
         BEGIN
             IF @name NOT LIKE '%log%'
                 SELECT @alter_command = 'ALTER DATABASE [tempdb] MODIFY FILE ( NAME =N'''+@name+''', SIZE = '+CAST(@size AS NVARCHAR)+'MB, FILEGROWTH = '+CAST(@growth AS NVARCHAR)+'MB )';
                 ELSE
             SELECT @alter_command = 'ALTER DATABASE [tempdb] MODIFY FILE ( NAME =N'''+@name+''', SIZE = '+CAST(@logsize AS NVARCHAR)+'MB, FILEGROWTH = '+CAST(@growth AS NVARCHAR)+'MB )';
             IF @PrintOnly = 1
                 BEGIN
                     PRINT @alter_command;
                 END;
                 ELSE
                 BEGIN
                     PRINT @alter_command;
                     EXEC sp_executesql
                          @alter_command;
                 END;
             FETCH NEXT FROM db_cursor INTO @name;
         END;
     CLOSE db_cursor;
     DEALLOCATE db_cursor;
GO
