USE [master];
GO
DECLARE @cpu_count INT, @file_count INT, @logical_name SYSNAME, @file_name NVARCHAR(520), @physical_name NVARCHAR(520), @size INT, @logsize INT, @percent_free DECIMAL(4, 2), @max_size INT, @growth INT, @alter_command NVARCHAR(MAX), @instance_count INT;
SET @instance_count = 2; --Total number of instances sharing same tempdb drive
SET @percent_free = 25.00;  --Percent disk space free
SELECT @physical_name = physical_name,
       @size = size / 128,
       @max_size = max_size / 128,
       @growth = growth / 128
FROM tempdb.sys.database_files
WHERE name = 'tempdev';
SELECT @file_count = COUNT(*)
FROM tempdb.sys.database_files
WHERE type_desc = 'ROWS';
SELECT @cpu_count = cpu_count
FROM sys.dm_os_sys_info;
PRINT 'Total CPU Count: '+CONVERT(VARCHAR(4), @cpu_count);
PRINT 'Total File Count: '+CONVERT(VARCHAR(4), @file_count);
IF @cpu_count > 8
    SET @cpu_count = 8;
WHILE @file_count < @cpu_count -- Add * 0.25 here to add 1 file for every 4 cpus, * .5 for every 2 etc.
    BEGIN
        SELECT @logical_name = 'tempdev'+CAST(@file_count AS NVARCHAR);
        SELECT @file_name = REPLACE(@physical_name, 'tempdb.mdf', @logical_name+'.ndf');
        SELECT @alter_command = 'ALTER DATABASE [tempdb] ADD FILE ( NAME =N'''+@logical_name+''', FILENAME =N'''+@file_name+''', SIZE = '+CAST(@size AS NVARCHAR)+'MB, FILEGROWTH = '+CAST(@growth AS NVARCHAR)+'MB )';
        PRINT @alter_command;
   -- EXEC    sp_executesql @alter_command

        SELECT @file_count = @file_count + 1;
    END;
SELECT @size = total_bytes / 1024 / 1024 / (@file_count + 1) * (100 - @percent_free) / 100 / @instance_count
FROM sys.dm_os_volume_stats(2, 1);
DECLARE @name NVARCHAR(128);
PRINT 'Data file size is '+CONVERT(VARCHAR(8), @size);
DECLARE db_cursor CURSOR
FOR
    SELECT name
    FROM sys.master_files
    WHERE database_id = 2
          AND size / 128.0 <= @size - 1;
IF(@size > 20000)
    SET @growth = 4000;
    ELSE
SET @growth = 2000;
IF(@size > 8000)
    SET @logsize = 8000;
    ELSE
SET @logsize = @size;
OPEN db_cursor;
FETCH NEXT FROM db_cursor INTO @name;
WHILE @@FETCH_STATUS = 0
    BEGIN
        IF @name NOT LIKE '%log%'
            SELECT @alter_command = 'ALTER DATABASE [tempdb] MODIFY FILE ( NAME =N'''+@name+''', SIZE = '+CAST(@size AS NVARCHAR)+'MB, FILEGROWTH = '+CAST(@growth AS NVARCHAR)+'MB )';
            ELSE
        SELECT @alter_command = 'ALTER DATABASE [tempdb] MODIFY FILE ( NAME =N'''+@name+''', SIZE = '+CAST(@logsize AS NVARCHAR)+'MB, FILEGROWTH = '+CAST(@growth AS NVARCHAR)+'MB )';
        PRINT @alter_command;
        EXEC sp_executesql
             @alter_command;
        FETCH NEXT FROM db_cursor INTO @name;
    END;
CLOSE db_cursor;
DEALLOCATE db_cursor;
GO
